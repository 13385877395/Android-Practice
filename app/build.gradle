apply plugin: 'com.android.application'

ext {
    SUPPORT_LIBRARY_VERSION = "25.4.0"
}

repositories {
    maven {
        url "https://maven.google.com"
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "com.farmerbb.notepad"
        minSdkVersion 9
        targetSdkVersion 25

        versionCode 94
        versionName "2.2.5"

        resConfigs "en", "fr", "ko", "nl", "pl", "zh-rCN"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            if(System.getenv("KSTOREFILE") != null)
                storeFile file(System.getenv("KSTOREFILE"))

            storePassword System.getenv("KSTOREPWD")
            keyAlias System.getenv("KEYALIAS")
            keyPassword System.getenv("KEYPWD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "@string/app_name_debug"]
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            signingConfig signingConfigs.release
            manifestPlaceholders = [appName: "@string/app_name"]
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:design:${SUPPORT_LIBRARY_VERSION}"
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'us.feras.mdv:markdownview:1.1.0'
}
